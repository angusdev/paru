plugins {
  id 'application' 
  id 'com.github.johnrengelman.shadow' version '7.1.2'
  id 'eclipse'
}

repositories {
  mavenCentral() 
}

dependencies {
  implementation 'org.apache.poi:poi-ooxml:3.15'
  implementation 'net.lingala.zip4j:zip4j:1.3.2'
  implementation 'com.itextpdf:itextpdf:5.5.10'
  implementation 'org.bouncycastle:bcprov-jdk15on:1.56'
  
  testImplementation 'org.testng:testng:6.8.8'
}

application {
  mainClass = 'org.ellab.passwordrecovery.Main'
}

group = "org.ellab.paru"
version = "0.5"

description = "Password Recovery Utility"

repositories {
  mavenCentral()
}

test {
  useTestNG() {
  	suites file('src/test/resources/testng.xml')
  }
 
  // listen to events in the test execution lifecycle
  beforeTest { descriptor ->
     logger.lifecycle("Running test: " + descriptor)
  }

  // listen to standard out and standard error of the test JVM(s)
  onOutput { descriptor, event ->
     logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
  }
}

jar {
  manifest {
    attributes 'Main-Class': application.mainClass
  }
}

task srcZip(type: Zip) {
  classifier = 'src'
  from projectDir
  include 'build.gradle', 'src/**/*'
}

run {
  if (project.hasProperty('args')) {
    args project.args.split('\\s+')
  }
}
